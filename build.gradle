plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.huibur.furion'
version = '1.0.3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    swagger2Ver = '2.9.2'
    swaggerUIVer = '2.9.2'
    swaggerAnnot = '1.5.21'
    swaggerModel = '1.5.21'
}

dependencies {
    implementation ("io.springfox:springfox-swagger2:$swagger2Ver") {
        exclude group:'io.swagger', module: 'swagger-annotations'
        exclude group:'io.swagger', module: 'swagger-models'
    }
    implementation "io.springfox:springfox-swagger-ui:$swaggerUIVer"
    compile group: 'io.swagger', name: 'swagger-annotations', version:swaggerAnnot
    compile group: 'io.swagger', name: 'swagger-models', version: swaggerModel
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'furion-swagger'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'furion swagger'
                description = 'swagger config'
                url = 'https://github.com/furion-component/furion-swagger.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                organization {
                    name = 'HuiBur'
                    url = 'https://www.huibur.com'
                }
                developers {
                    developer {
                        id = 'wenzhijie'
                        name = 'wenzhijie'
                        email = 'wenzhi_jie@126.com'
                        organization = 'HuiBur'
                        organizationUrl = 'https://www.huibur.com'
                        roles = ['owner', 'developer']
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/furion-component/furion-swagger'
                    developerConnection = 'scm:git:ssh://git@github.com:furion-component/furion-swagger.git'
                    url = 'https://github.com/furion-component/furion-swagger.git'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
//    if (JavaVersion.current().isJava9Compatible()) {
//        options.addBooleanOption('html5', true)
//    }
}