plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.huibur.furion'
version = '1.0.0'

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'com.huibur.furion:furion-common:0.0.4-SNAPSHOT'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'furion-base'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'furion base'
                description = 'a base module for java web'
                url = 'https://github.com/furion-component/Furion-Base.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'wenzhijie'
                        name = 'wenzhijie'
                        email = 'wenzhi_jie@126.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/furion-component/Furion-Base'
                    developerConnection = 'scm:git:ssh://git@github.com:furion-component/Furion-Base.git'
                    url = 'https://github.com/furion-component/Furion-Base.git'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}